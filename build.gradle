plugins {
    id 'com.gorylenko.gradle-git-properties' version '2.2.2'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.devsoap.vaadin-flow' version '1.3.5'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'
	id 'java'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '2.3.0'
    id 'maven-publish'
    id 'com.github.spotbugs' version '4.2.4'
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
}

spotbugs {
    toolVersion = '4.0.3'
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'low'
    showProgress = true
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
        }
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.xml")
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jacoco {
	toolVersion = "0.8.5"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
		csv.enabled = false
	}
}

vaadin {
    version = '13.0.13'
    compatibilityMode = true
}

node {
  version = '14.2.0'
  npmVersion = '6.14.5'
  yarnVersion = '1.22.4'
  distBaseUrl = 'https://nodejs.org/dist'
  download = true
  workDir = file("${project.buildDir}/nodejs")
  npmWorkDir = file("${project.buildDir}/npm")
  yarnWorkDir = file("${project.buildDir}/yarn")
  nodeModulesDir = file("${project.projectDir}")
}


group = 'io.pivotal'
version = '0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11

repositories {
	mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
	//vaadin.repositories()
}

ext {
	  set('springCloudServicesVersion', '3.1.2.RELEASE')
	  set('springCloudVersion', "2020.0.0-M1")
	  set('spring-cloud-services-connectors.version', '3.1.2.RELEASE')
	  // @see https://github.com/spring-projects/spring-boot/blob/master/spring-boot-project/spring-boot-dependencies/pom.xml
      set('spring-data-releasetrain.version', 'Neumann-RELEASE')
      set('resilience4j.version', '1.4.0')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:${springCloudServicesVersion}"
		mavenBom "com.vaadin:vaadin-bom:${vaadin.version}"
	}
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

configurations {
	// exclude Logback and Ribbon
    implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    implementation.exclude group: 'org.springframework.cloud', module: 'spring-cloud-starter-netflix-ribbon'
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
	}
}

dependencies {
	annotationProcessor('org.projectlombok:lombok')
	implementation('org.projectlombok:lombok')
	implementation('org.apache.commons:commons-lang3')
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	runtimeOnly('org.springframework.boot:spring-boot-devtools')
	implementation('org.springframework.boot:spring-boot-starter-log4j2')
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation('org.springframework.boot:spring-boot-starter-webflux')
	implementation('io.projectreactor:reactor-core:3.3.5.RELEASE')
    implementation('io.projectreactor.netty:reactor-netty:0.9.7.RELEASE')
    implementation vaadin.core()
	implementation('com.vaadin:vaadin-spring-boot-starter')
	//implementation('com.github.appreciated:apexcharts:0.8.0')
	implementation('org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j')
    implementation('io.github.resilience4j:resilience4j-annotations:1.4.0')
	implementation('io.pivotal.spring.cloud:spring-cloud-services-starter-service-registry')
	implementation('io.micrometer:micrometer-registry-prometheus')
	testImplementation('io.projectreactor:reactor-test')
	testImplementation('org.junit.jupiter:junit-jupiter-api')
	testImplementation('org.junit.jupiter:junit-jupiter-params')
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
	testImplementation('org.mockito:mockito-core')
	testImplementation('org.mockito:mockito-junit-jupiter')
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
	    exclude group: 'junit', module: 'junit'
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'cf-hoover-ui'
                url = 'https://github.com/pacphi/cf-hoover-ui'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pacphi'
                        name = 'Chris Phillipson'
                        email = 'cphillipson@pivotal.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pacphi/cf-hoover-ui.git'
                    developerConnection = 'scm:git:ssh://github.com/pacphi/cf-hoover-ui.git'
                    url = 'https://github.com/pacphi/cf-hoover-ui'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

bootJar.enabled = true
jar.enabled = false

test.finalizedBy jacocoTestReport
